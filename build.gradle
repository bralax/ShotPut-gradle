plugins {
   id 'java-gradle-plugin'
   id 'java-library'
   id 'maven-publish'
   id 'signing'
   id 'com.gradle.plugin-publish' version '0.19.0'
}

group='io.github.bralax'
version='0.2.2'
gradlePlugin {
    plugins {
        shotputPlugin {
            id = 'io.github.bralax.shotput'
            implementationClass = 'io.github.bralax.shotput.gradle.ShotputPlugin'
            displayName='ShotPut Plugin'
            description='A plugin for integrating a gradle project with ShotPut'
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation gradleApi()
    implementation group: 'io.github.bralax', name: 'shotput', version: '0.4.1'
}

java {
    withJavadocJar()
    withSourcesJar()
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }

}

pluginBundle {
    website='https://github.com/bralax/ShotPut-gradle'
    vcsUrl='https://github.com/bralax/ShotPut-gradle.git'
    tags=['Javalin', 'Documentation', 'ShotPut', 'API']
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId='io.github.bralax'
            artifactId = 'shotput-gradle'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'ShotPut Gradle'
                description = 'A gradle plugin for using ShotPut'
                url = 'https://github.com/bralax/ShotPut-gradle'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }
                developers {
                    developer {
                        id = 'bralax'
                        name = 'Brandon Lax'
                        email = 'bralax12@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://www.github.com/bralax/ShotPut-gradle.git'
                    developerConnection = 'scm:git:ssh://www.github.com/bralax/ShotPut-gradle.git'
                    url = 'http://ewww.github.com/bralax/ShotPut-gradle'
                }
            }
        }
    }
    repositories {
        maven {
            // change URLs to point to your repos, e.g. http://my.org/repo
            def releasesRepoUrl = layout.buildDirectory.dir('repos/releases')
            def snapshotsRepoUrl = layout.buildDirectory.dir('repos/snapshots')
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
   sign publishing.publications.mavenJava
   useGpgCmd()
   sign configurations.archives
}


javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
